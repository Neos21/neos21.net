---
title        : Windows の游ゴシック Regular・Light を Medium フォントに差し替えて太く見せる VBScript
created      : 2019-02-24
last-modified: 2019-02-24
header-date  : true
path:
  - /index.html Neo's World
  - /blog/index.html Blog
  - /blog/2019/index.html 2019年
  - /blog/2019/02/index.html 02月
hidden-info:
  original-blog: Corredor
---

*Windows で游ゴシックフォントが細く見える問題*に終止符。

- [ウェブサイトに適用する游ゴシックフォントを見直しまくった最終解](/blog/2019/01/05-02.html)
- [Windows・Chrome で游ゴシックフォントを少しだけ太く見せる JavaScript と CSS](/blog/2019/01/27-01.html)

Windows に搭載されている「游ゴシック Regular」と「游ゴシック Light」について、*フォント名とフォントファイルの関連付けを行っているレジストリを書き換え*てしまい、どちらの場合も**「游ゴシック Medium」フォントで表示させてしまおう**、という目論見。

手作業での手順は以下の記事で紹介したが、今回はコレを VBScript にした。

- [ウェブサイトに適用する游ゴシックフォントを見直しまくった最終解](/blog/2019/01/05-02.html) … レジストリをイジって Regular・Light ウェイトのフォントを Medium フォントに置換する

以下がその VBScript だ。コレを `ForceYuGothicMedium.vbs` とかいう名前で保存しよう。

```vb
Option Explicit

' 「游ゴシック Regular」「游ゴシック Light」のフォントを「游ゴシック Medium」のフォントファイルに差し替えて
' Windows 環境の游ゴシックを太めにするスクリプト
' 
' - 「游ゴシック Medium」のフォントファイルをコピーして用意してレジストリを書き換えます
' - 実行後、一度サインアウトして再度サインインすると変更が反映されます
' - 管理者権限がないと WScript.Shell の regWrite 関数で「レジストリ キー "…" のルートが無効です。」エラーが出るので
'   管理者権限で開いたコマンドプロンプトより `cscript ForceYuGothicMedium.vbs` と実行してください

' Object の用意
Dim shell : Set shell = WScript.CreateObject("WScript.Shell")
Dim fs    : Set fs    = WScript.CreateObject("Scripting.FileSystemObject")

' レジストリキーの宣言
Dim fontsKey           : fontsKey           = "HKLM\SOFTWARE\Microsoft\Windows NT\CurrentVersion\Fonts\"
Dim yuGothicRegularKey : yuGothicRegularKey = fontsKey & "Yu Gothic Regular & Yu Gothic UI Semilight (TrueType)"
Dim yuGothicLightKey   : yuGothicLightKey   = fontsKey & "Yu Gothic Light & Yu Gothic UI Light (TrueType)"

' 変更前のレジストリ値を読んでおく
Dim yuGothicRegularValue : yuGothicRegularValue = shell.RegRead(yuGothicRegularKey)
Dim yuGothicLightValue   : yuGothicLightValue   = shell.RegRead(yuGothicLightKey)

WScript.Echo("変更前の YuGothic Regular フォントファイル名 : " & yuGothicRegularValue)
WScript.Echo("変更前の YuGothic Light フォントファイル名   : " & yuGothicLightValue)

' YuGothic Medium フォント
Dim fontsPath          : fontsPath      = "C:\Windows\Fonts\"
Dim yuGothicMediumFile : yuGothicMedium = "YuGothM.ttc"
' 以下のファイル名でコピーを配置する
Dim yuGothicMediumForRegular : yuGothicMediumForRegular = "YuGothM2.ttc"
Dim yuGothicMediumForLight   : yuGothicMediumForLight   = "YuGothM3.ttc"

' ファイルコピー実行
On Error Resume Next
Call fs.CopyFile(fontsPath & yuGothicMediumFile, fontsPath & yuGothicMediumForRegular)
Call fs.CopyFile(fontsPath & yuGothicMediumFile, fontsPath & yuGothicMediumForLight)
On Error GoTo 0
WScript.Echo(yuGothicMediumForRegular & " ファイルのコピー生成完了")
WScript.Echo(yuGothicMediumForLight   & " ファイルのコピー生成完了")

' レジストリ値を変更する
shell.RegWrite yuGothicRegularKey, yuGothicMediumForRegular, "REG_SZ"
shell.RegWrite yuGothicLightKey  , yuGothicMediumForLight  , "REG_SZ"
WScript.Echo("ファイルコピー・レジストリ変更完了")
```

- [windows-batch-scripts/ForceYuGothicMedium.vbs at master · Neos21/windows-batch-scripts · GitHub](https://github.com/Neos21/shell-scripts/blob/master/vbscript/force-yu-gothic-medium.vbs)

次に、管理者権限でコマンドプロンプトか PowerShell を開く。`Win + X` キーを押下して現れるメニューから選択すると早いだろう。

`ForceYuGothicMedium.vbs` ファイルを保存したディレクトリに移動し、`cscript` コマンドで実行する。管理者権限がないと、`RegWrite` 関数でエラーが発生してしまう。

```batch
> cscript ForceYuGothicMedium.vbs
```

コレで OK。あとは一度サインアウトしてから再度サインインすれば、游ゴシック Regular・游ゴシック Light が使われていたところに、游ゴシック Medium が適用されるようになっているはずだ。OS 環境からまるっと Regular・Light フォントを葬ってしまうので、ブラウザやソフトを問わず効果がある。

スクリプトを見てもらえば分かるが、参照するフォントファイル名を切り替えているだけで、Regular・Light ウェイトのフォントファイル自体は残してあるので、レジストリをイジれば元に戻せる。
