---
title        : VBScript でプログレスバー的なモノを作りたい
created      : 2016-06-24
last-modified: 2016-06-24
header-date  : true
path:
  - /index.html Neo's World
  - /blog/index.html Blog
  - /blog/2016/index.html 2016年
  - /blog/2016/06/index.html 06月
hidden-info:
  original-blog: Corredor
---

VBScript で進捗状況を示すプログレスバー的なモノを作りたい。

それらしい API はないようなので、IE を起動させ、処理中にメッセージを表示させていくことにする。

```vb
' IE の宣言と初期化
Dim ie
initializeIe()

updateMsg "処理を開始します..."

' 処理…
updateMsg "○○処理を実行しています..."
WScript.Sleep 5000  ' サンプル用・処理している風に少し止める

' 完了
updateMsg "スクリプト処理完了!"
MsgBox "スクリプト処理が完了しました。"

' IE を終了させスクリプトを閉じる
closeIe()
WScript.Quit



' IE の初期化
Sub initializeIe()
  Set ie = CreateObject("InternetExplorer.Application")
  With ie
    .Navigate("about:blank")
    .ToolBar = False
    .StatusBar = False
    ' 幅・高さの設定
    .Width = 300
    .Height = 200
    ' 画面右上に配置する。"parentWindow.screen" はパスカルケースで書くと認識されない
    .Top = 0
    .Left = .Document.parentWindow.screen.Width - 300
    .Document.Charset = "UTF-8"
    .Visible = True
    .Document.Title = "スクリプト実行中"
  End With
End Sub

' メッセージを IE に追記する
' 
' 引数のメッセージを IE の最終行に追記する。
' 最終行が表示されるようにスクロール位置を最下部に設定する。
Sub updateMsg(value)
  With ie
    .Document.Body.innerHTML = .Document.Body.innerHTML & value & "<br>"
    .Document.Script.setTimeout "javascript:scrollTo(0," & .Document.Body.ScrollHeight & ");", 0
  End With
End Sub

' IE を終了する
Sub closeIe()
  ie.Quit
  Set ie = Nothing
End Sub
```

小さめのウィンドウの IE を画面右上に配置し、処理中にメッセージを追記していくことで、進捗状況を示すようにする。

変数 `ie` をプロシージャ内から操作できるように、`Dim ie` はグローバル変数として定義しておく。初期化と終了処理は扱いやすいようにプロシージャに外出ししておいた。

メッセージの追記もプロシージャにした。関数名を短くしておけば、デバッグログというか、コメントの代わりとして使えるかなと思い。IE でのメッセージ表示が必要なくなった時は何も処理しない関数にしてしまえば良いし。

`parentWindow.screen` は `ParentWindow.Screen` などとパスカルケースで書くとエラーになる。ココは JavaScript チックな DOM オブジェクトを見ているってことなのかな。`Document` とか `Width` とかはパスカルケースでもキャメルケースでも良いのに。これらは VBScript 側で同じオブジェクトを持っているってことなのかしら。よく知らない。

スクロールバーの追随は、VBScript から IE に向けて JavaScript を投げて、`scrollTo()` を呼び出すようにしている。

## 作るにあたって参考にしたページ

- [Hey, Scripting Guy! : スクリプトの実行中に進行状況バー (またはそれに似たもの) を表示する方法はありますか](https://gallery.technet.microsoft.com/scriptcenter/58a40515-e502-47d2-99bf-d5679b7976ab)
- [ディスプレイの解像度を取得する - SO NOTE そうのて (；^ω^)](http://d.hatena.ne.jp/so_blue/20111004/1317735073)
- [WindowsでInternet Explorerを使って文字コードを変換する：Tech TIPS - ＠IT](http://www.atmarkit.co.jp/ait/articles/0402/28/news004.html)
- <http://efcit.co.jp/office2000/exqalounge.cgi?print+200901/09010017.txt>
